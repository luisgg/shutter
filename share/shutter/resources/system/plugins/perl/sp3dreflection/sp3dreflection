#! /usr/bin/env perl

###################################################
#
#  Copyright (C) 2008-2012 Mario Kemper <mario.kemper@googlemail.com> and Shutter Team
#
#  This file is part of Shutter.
#
#  Shutter is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 3 of the License, or
#  (at your option) any later version.
#
#  Shutter is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with Shutter; if not, write to the Free Software
#  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
#
###################################################

###################################################
#
#  Based on a script by Fred Weinhaus
#
#  Fred Weinhaus and http://www.fmwconcepts.com/imagemagick/index.html
#
#  http://www.fmwconcepts.com/imagemagick/3Dreflection/index.php
#
###################################################

use utf8;
use strict;
use warnings;
use Gtk2 '-init';
use Image::Magick;
use POSIX qw/setlocale/;
use Locale::gettext;
use Glib qw/TRUE FALSE/;
use FindBin '$Bin';    #path where plugin is located
use File::Temp qw/ tempfile tempdir /;
use Time::HiRes qw/usleep/;
use Data::Dumper;

#load modules at custom path at runtime
#--------------------------------------
require lib;
import lib $ENV{'SHUTTER_ROOT'}."/share/shutter/resources/modules";

#proc (Thanks to Michael Schilli)
require Proc::Simple;

#load shutter's modules
#--------------------------------------
require Shutter::App::SimpleDialogs;

#Set LC_NUMERIC to C to prevent decimal commas (or anything else)
setlocale(LC_NUMERIC, "C");	
#configure gettext using ENV Variable (setup during shutter start)
setlocale( LC_MESSAGES, "" );
my $d = Locale::gettext->domain("shutter-plugins");
$d->dir( $ENV{'SHUTTER_INTL'} );

#icontheme to determine if icons exist or not
#in some cases we deliver fallback icons
my $icontheme = Gtk2::IconTheme->get_default;
$icontheme->append_search_path($ENV{'SHUTTER_ROOT'} . "/share/icons");

#shutter will ask for some infos
my %plugin_info = 	( 	'name'	=> $d->get( "3D reflection" ),
						'sort'	=> $d->get( "Effect" ),
						'tip'	=> $d->get("Adds a fading reflection below an image and views it in perspective\n\nBased on a script by Fred Weinhaus\n\nhttp://www.fmwconcepts.com/imagemagick/3Dreflection/index.php"),
					);

binmode( STDOUT, ":utf8" );
if ( exists $plugin_info{$ARGV[ 0 ]} ) {
	print $plugin_info{$ARGV[ 0 ]};
	exit;
}

#these variables are passed to the plugin
my $socket_id = $ARGV[ 0 ];
my $filename  = $ARGV[ 1 ];
my $width     = $ARGV[ 2 ];
my $height    = $ARGV[ 3 ];
my $filetype  = $ARGV[ 4 ];

#decode filename
utf8::decode $filename;

my $plug = Gtk2::Plug->new( $socket_id );
$plug->set_default_icon_name( 'shutter' );
$plug->set_border_width( 10 );

$plug->signal_connect( destroy => sub { Gtk2->main_quit } );

#tooltips
my $tooltips = Gtk2::Tooltips->new;

#configure buttons and other needed controls
my $rotation_label = Gtk2::Label->new( $d->get("Rotation:") );
my $rotation_sbutton = Gtk2::SpinButton->new_with_range( 0, 90, 1 );
$rotation_sbutton->set_value( 20 );

$tooltips->set_tip(
	$rotation_label,
	$d->get(
		"Perseptive rotation around left edge of image"
	)
);

$tooltips->set_tip(
	$rotation_sbutton,
	$d->get(
		"Perseptive rotation around left edge of image"
	)
);

my $fade_label = Gtk2::Label->new( $d->get("Fade:") );
my $fade_sbutton = Gtk2::SpinButton->new_with_range( 0, 100, 1 );
$fade_sbutton->set_value( 40 );

$tooltips->set_tip(
	$fade_label,
	$d->get(
		"Fade percent for mirror image"
	)
);

$tooltips->set_tip(
	$fade_sbutton,
	$d->get(
		"Fade percent for mirror image"
	)
);

my $opacity_label = Gtk2::Label->new( $d->get("Opacity:") );
my $opacity_sbutton = Gtk2::SpinButton->new_with_range( 0, 100, 1 );
$opacity_sbutton->set_value( 100 );

$tooltips->set_tip(
	$opacity_label,
	$d->get(
		"Opacity percent applied to whole mirror image"
	)
);

$tooltips->set_tip(
	$opacity_sbutton,
	$d->get(
		"Opacity percent applied to whole mirror image"
	)
);

my $pef_label = Gtk2::Label->new( $d->get("Pef:") );
my $pef_sbutton = Gtk2::SpinButton->new_with_range( 0, 3, 0.01 );
$pef_sbutton->set_value( 1 );

$tooltips->set_tip(
	$pef_label,
	$d->get(
		"Perspective exaggeration factor"
	)
);

$tooltips->set_tip(
	$pef_sbutton,
	$d->get(
		"Perspective exaggeration factor"
	)
);

my $gap_label = Gtk2::Label->new( $d->get("Gap:") );
my $gap_sbutton = Gtk2::SpinButton->new_with_range( 0, 1000, 1 );
$gap_sbutton->set_value( 0 );

$tooltips->set_tip(
	$gap_label,
	$d->get(
		"Gap between image and mirror in pixels"
	)
);

$tooltips->set_tip(
	$gap_sbutton,
	$d->get(
		"Gap between image and mirror in pixels"
	)
);

my $zoom_label = Gtk2::Label->new( $d->get("Zoom:") );
my $zoom_sbutton = Gtk2::SpinButton->new_with_range( 0, 10, 0.10 );
$zoom_sbutton->set_value( 1 );

$tooltips->set_tip(
	$zoom_label,
	$d->get(
		"Zoom factor for output image"
	)
);

$tooltips->set_tip(
	$zoom_sbutton,
	$d->get(
		"Zoom factor for output image"
	)
);

my $back_color_label = Gtk2::Label->new( $d->get("Background color"). ":" );
my $back_color = Gtk2::ColorButton->new();
$back_color->set_color( Gtk2::Gdk::Color->parse('gray') );
$back_color->set_alpha( int( 0 * 65535 ) );
$back_color->set_use_alpha(TRUE);
$back_color->set_title( $d->get("Choose background color") );

#all labels on the left side 
#need to have the same size
my $sgl = Gtk2::SizeGroup->new ('both');
$sgl->set ('ignore-hidden' => FALSE);
$sgl->add_widget($rotation_label);
$sgl->add_widget($opacity_label);
$sgl->add_widget($fade_label);
$sgl->add_widget($pef_label);
$sgl->add_widget($gap_label);
$sgl->add_widget($zoom_label);

#we define two Gtk2::Image widgets
#to store the screenshot
#and a throbber that is shown while the changes are processed
my $preview =
	Gtk2::Image->new_from_pixbuf(
	   Gtk2::Gdk::Pixbuf->new_from_file_at_scale( $filename, 300, 300, TRUE ) );

my $preview_throb =
	Gtk2::Image->new_from_file( $ENV{'SHUTTER_ROOT'}."/share/shutter/resources/icons/throbber.gif" );

my $sg = Gtk2::SizeGroup->new ('both');
$sg->set ('ignore-hidden' => FALSE);
$sg->add_widget($preview);
$sg->add_widget($preview_throb);


#we define three Gtk2::Button widgets
#to refresh, save and cancel the plugin's work
my $refresh_btn = Gtk2::Button->new_from_stock( 'gtk-refresh' );
$refresh_btn->signal_connect( 'clicked', \&fct_imagemagick_3Dreflection,
							  'refresh' );

my $save_btn = Gtk2::Button->new_from_stock( 'gtk-save' );
$save_btn->signal_connect( 'clicked', \&fct_imagemagick_3Dreflection, 'save' );

my $cancel_btn = Gtk2::Button->new_from_stock( 'gtk-cancel' );
$cancel_btn->signal_connect( 'clicked' => sub { Gtk2->main_quit; exit 2; }, 'cancel' );

#define the gui layout
my $hbox1      = Gtk2::HBox->new( FALSE, 8 );
my $hbox2      = Gtk2::HBox->new( FALSE, 8 );
my $hbox3      = Gtk2::HBox->new( FALSE, 8 );
my $hbox4      = Gtk2::HBox->new( FALSE, 8 );
my $hbox5      = Gtk2::HBox->new( FALSE, 8 );
my $hbox6      = Gtk2::HBox->new( FALSE, 8 );
my $hbox7      = Gtk2::HBox->new( FALSE, 8 );

my $hbox_row1 = Gtk2::HBox->new( TRUE, 8 );
my $hbox_row2 = Gtk2::HBox->new( TRUE, 8 );

my $vbox_param = Gtk2::VBox->new( FALSE,  8 );
my $vbox_left = Gtk2::VBox->new( FALSE, 8 );
my $vbox_right = Gtk2::VBox->new( FALSE, 8 );

my $hbox_btn  = Gtk2::HBox->new( TRUE,  8 );
my $vbox_btn  = Gtk2::VBox->new( FALSE, 8 );
my $vbox_main = Gtk2::VBox->new( FALSE, 8 );

#packing
$hbox1->pack_start( $rotation_label, FALSE, TRUE, 5 );
$hbox1->pack_start( $rotation_sbutton, TRUE,  TRUE, 5 );

$hbox2->pack_start( $fade_label, FALSE, TRUE, 5 );
$hbox2->pack_start( $fade_sbutton, TRUE,  TRUE, 5 );

$hbox3->pack_start( $opacity_label, FALSE, TRUE, 5 );
$hbox3->pack_start( $opacity_sbutton, TRUE,  TRUE, 5 );

$hbox4->pack_start( $pef_label, FALSE, TRUE, 5 );
$hbox4->pack_start( $pef_sbutton, TRUE,  TRUE, 5 );

$hbox5->pack_start( $gap_label, FALSE, TRUE, 5 );
$hbox5->pack_start( $gap_sbutton, TRUE,  TRUE, 5 );

$hbox6->pack_start( $zoom_label, FALSE, TRUE, 5 );
$hbox6->pack_start( $zoom_sbutton, TRUE,  TRUE, 5 );

$hbox7->pack_start( $back_color_label, FALSE, TRUE, 5 );
$hbox7->pack_start( $back_color, TRUE,  TRUE, 5 );

#row 1
$hbox_row1->pack_start_defaults( $hbox7 );

#controls on the left side
$vbox_left->pack_start_defaults( $hbox1 );
$vbox_left->pack_start_defaults( $hbox2 );
$vbox_left->pack_start_defaults( $hbox3 );
$vbox_left->pack_start_defaults( $hbox4 );
$vbox_left->pack_start_defaults( $hbox5 );
$vbox_left->pack_start_defaults( $hbox6 );

#preview
$vbox_right->pack_start_defaults( $preview );
$vbox_right->pack_start_defaults( $preview_throb );

#row 2
$hbox_row2->pack_start_defaults( $vbox_left );
$hbox_row2->pack_start_defaults( $vbox_right );

$vbox_param->pack_start( $hbox_row1, TRUE, TRUE, 5 );
$vbox_param->pack_start( $hbox_row2, TRUE, TRUE, 5 );

$vbox_main->pack_start( $vbox_param, FALSE, TRUE, 5 );
$vbox_main->pack_start( $refresh_btn, TRUE, TRUE, 5 );

$hbox_btn->pack_start( $cancel_btn, TRUE, TRUE, 5 );
$hbox_btn->pack_start( $save_btn,   TRUE, TRUE, 5 );

$vbox_main->pack_start( $hbox_btn,  TRUE, TRUE, 5 );

$plug->add( $vbox_main );

$plug->show_all;

#hide the preview widget at startup 
$preview->hide_all;

#create tempfile
my ( $tmpfh, $tmpfilename ) = tempfile();
#png format
$tmpfilename .= ".png";

#we fork a child process to do the work
my $process = Proc::Simple->new;
#create tempfiles for subprocess outputs
my ( $tmpfh_stdout, $tmpfilename_stdout ) = tempfile(UNLINK => 1);
my ( $tmpfh_stderr, $tmpfilename_sterr ) = tempfile(UNLINK => 1);
$process->redirect_output ($tmpfilename_stdout, $tmpfilename_sterr);

#generate first preview at startup
Glib::Idle->add (
	sub{
		&fct_imagemagick_3Dreflection( undef, 'refresh' );
		return FALSE;	
	}, 
undef);

#lets'start
Gtk2->main;


sub fct_imagemagick_3Dreflection {
	my ( $widget, $data ) = @_;

	$save_btn->set_sensitive(FALSE);

	if ( $data eq 'save' ) {

		my $image = Image::Magick->new;
		$image->ReadImage( $tmpfilename );
		$image->WriteImage( filename => $filename );

		#delete temp files
		unlink $tmpfilename;
		
		Gtk2->main_quit;
		return TRUE;
	} else {

		$preview->hide_all;
		$preview_throb->show_all;

		$process->start(
			sub {
				&apply_effect();
				POSIX::_exit(0);
			}
		);

		#so we can update the gui
		while ( $process->poll ) {
			&fct_update_gui;
			usleep 100000;
		}
		
		eval{
			$preview->set_from_pixbuf( Gtk2::Gdk::Pixbuf->new_from_file_at_scale(
										$tmpfilename, 300, 300, TRUE)
								 	 );
		};
		#error -> read output from tempfiles
		if($@){		
			#store error here
			my $error_string = "Plugin error:\n";
			
			#reading stdout from file
			while (<$tmpfh_stdout>){
				$error_string .= $_;	
			}
			#reading stderr from file
			while (<$tmpfh_stderr>){
				$error_string .= $_;	
			}

			#get the parent window of the plug
			require X11::Protocol;
			my $x11 = X11::Protocol->new( $ENV{ 'DISPLAY' } );
			
			my $plugp = Gtk2::Gdk::Window->foreign_new( &find_wm_window( $x11, $plug->get_id ));
			
			#show error message
			my $shutter_dialog = Shutter::App::SimpleDialogs->new($plug, $plugp);
			$shutter_dialog->dlg_error_message( 
				sprintf ( $d->get(  "Error while executing plugin %s." ), "'" . $plugin_info{'name'} . "'" ) ,
				$d->get( "There was an error executing the plugin." ),
				undef, undef, undef,
				undef, undef, undef,
				$error_string
			);
			
			#delete temp files
			unlink $tmpfilename;

			Gtk2->main_quit;
			exit 1;	
		}

		$save_btn->set_sensitive(TRUE);
		$preview->show_all;
		$preview_throb->hide_all;

		return TRUE;
	}
}

sub apply_effect {	

	my $bcolor = $back_color->get_color;

	#quote filename
	my $qfilename = quotemeta $filename;

	#check if script is executable
	unless ( -x $Bin."/3Dreflection"){
		my $changed = chmod(0755, $Bin."/3Dreflection");
		unless($changed){
			print "\nERROR: $Bin./3Dreflection is not executable\n";
			Gtk2->main_quit;
		}
	}
	
	#execute imagemagick command		
	system(   $Bin."/3Dreflection"
			. " -r "
			. $rotation_sbutton->get_value
			. " -f "
			. $fade_sbutton->get_value
			. " -o "
			. $opacity_sbutton->get_value
			. " -g "
			. $gap_sbutton->get_value
			. " -e "
			. $pef_sbutton->get_value										
			. " -z "
			. $zoom_sbutton->get_value
			. " -b '"
			. sprintf( "#%04x%04x%04x%04x", $bcolor->red, $bcolor->green, $bcolor->blue, $back_color->get_alpha )
			. "' $qfilename"
			. " $tmpfilename" );
	
}

sub fct_update_gui {

	while ( Gtk2->events_pending ) {
		Gtk2->main_iteration;
	}
	Gtk2::Gdk->flush;

	return TRUE;
}

sub find_wm_window {
	my $x11  = shift;
	my $xid  = shift;

	do {
		my ( $qroot, $qparent, @qkids ) = $x11->QueryTree($xid);
		return undef unless ( $qroot || $qparent );
		return $xid if ( $qroot == $qparent );
		$xid = $qparent;
	} while (TRUE);
}

1;

